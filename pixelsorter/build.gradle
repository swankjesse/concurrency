apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  buildToolsVersion rootProject.ext.buildToolsVersion
  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    applicationId 'com.publicobject.pixelsorter'

    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.compileSdkVersion

    versionCode 1
    versionName '1.0'

    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }
}

dependencies {
  compile libraries.kotlinStdLib

  androidTestCompile libraries.junit
  androidTestCompile libraries.supportRunner
  androidTestCompile libraries.supportRules
  androidTestCompile libraries.supportEspresso
}

// The default 'assemble' task only applies to normal variants. Add test variants as well.
android.testVariants.all { variant ->
  tasks.getByName('assemble').dependsOn variant.assemble
}

tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
  task.doFirst {
    logging.level = LogLevel.INFO
  }
  task.doLast {
    logging.level = LogLevel.LIFECYCLE
  }
}
